program -> declaration-list
declaration-list -> declaration declaration-list-prime
declaration-list-prime -> declaration declaration-list-prime | empty
declaration -> var-declaration | fun-declaration
var-declaration -> type-specifier ID ; | type-specifier ID [ NUM ] ;
type-specifier -> int | float | void
fun-declaration -> type-specifier ID ( params ) compound-stmt
params -> param-list | void
param-list -> param param-list-prime
param-list-prime -> , param param-list-prime | empty
param -> type-specifier ID | type-specifier ID [ ]
compound-stmt -> { local-declarations statement-list }
local-declarations -> var-declarations local-declarations | empty
statement-list -> statement statement-list | empty
statement -> expression-stmt | compound-stmt | selection-stmt | iteration-stmt | return-stmt
expression-stmt -> expression ; | ;
selection-stmt -> if ( expression ) statement | if ( expression ) statement else statement
iteration-stmt -> while ( expression ) statement
return-stmt -> return ; | return expression ;
expression -> var = expression | simple-expression
var -> ID | ID [ expression ]
simple-expression -> additive-expression relop additive-expression | additive-expression
relop -> <= | < | > | >= | == | !=
additive-expression -> term additive-expression-prime
additive-expression-prime -> addop term additive-expression-prime | empty
addop -> + | -
term -> factor term-prime
term-prime -> mulop factor term-prime | empty
mulop -> * | /
factor -> ( expression ) | var | call | NUM
call -> ID ( args )
args -> arg-list | empty
arg-list -> expression arg-list-prime
arg-list-prime -> , expression arg-list-prime | empty

Keywords: else if int return void while
Special symbols: + - * / < <= > >= == != = ; , ( ) [ ] { } /* */

ID = letter letter*
NUM = digit digit*
letter = a | .. | z | A | .. | Z 
digit = 0 | .. | 9